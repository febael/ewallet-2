plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
    id 'idea'
}

repositories {
    mavenCentral()
    mavenLocal()
}

def pippoVersion = '1.12.0'
def junitVersion = '5.4.2'
def jvmTargetVersion = '1.8'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "ro.pippo:pippo-core:$pippoVersion"
    implementation "ro.pippo:pippo-undertow:$pippoVersion"
    implementation "ro.pippo:pippo-controller:$pippoVersion"
    implementation "ro.pippo:pippo-gson:$pippoVersion"
    implementation "com.googlecode.cqengine:cqengine:3.2.0"
    implementation "ch.qos.logback:logback-classic:1.2.3"
    implementation "com.lmax:disruptor:3.4.2"
    implementation "com.google.inject:guice:4.2.2"
    implementation "it.ozimov:embedded-redis:0.7.2"
    implementation "redis.clients:jedis:3.0.1"

    testCompile "io.mockk:mockk:1.9.3"
    testCompile "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testCompile "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testCompile "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testCompile "com.github.mifmif:generex:1.0.2"
}

compileKotlin.kotlinOptions.jvmTarget = jvmTargetVersion
compileTestKotlin.kotlinOptions.jvmTarget = jvmTargetVersion

sourceSets {
    main.kotlin.srcDirs = ['src/main/resources', 'src/main/kotlin']
    test.kotlin.srcDirs = [
            'src/test/resources',
            'src/test/kotlin/unit',
            'src/test/kotlin/integration',
            'src/test/kotlin/performance',
            'src/test/kotlin/functional'
    ]
}

test {
    useJUnitPlatform()

    testLogging.showStandardStreams = true

    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

    failFast = true

    onOutput { descriptor, event ->
        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.bawer.tasks.revolut.ewallet.EWalletServiceApplicationKt'
    }
    from { [
            configurations.compile.collect { it.isDirectory() ? it : zipTree(it) },
            configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    ] }
    archiveBaseName = project.name + "-fat"
}